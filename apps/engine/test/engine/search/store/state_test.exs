defmodule Engine.Search.Store.StateTest do
  use ExUnit.Case, async: true

  import ExUnit.CaptureLog
  import Forge.Test.Fixtures

  alias Engine.Search.Store.State
  alias Forge.Project

  defmodule TimeoutBackend do
    @behaviour Engine.Search.Store.Backend

    def delete_by_path(_path) do
      exit({:timeout, {GenServer, :call, [:some_ref]}})
    end

    def new(_project), do: {:ok, :new}
    def prepare(_), do: {:ok, :empty}
    def insert(_entries), do: :ok
    def replace_all(_entries), do: :ok
    def find_by_subject(_subject, _type, _subtype), do: []
    def find_by_prefix(_prefix, _type, _subtype), do: []
    def find_by_ids(_ids, _type, _subtype), do: []
    def reduce(acc, _fun), do: acc
    def siblings(_entry), do: []
    def parent(_entry), do: nil
    def structure_for_path(_path), do: {:ok, %{}}
    def drop, do: :ok
    def destroy(_state), do: :ok
  end

  defmodule DeleteErrorBackend do
    @behaviour Engine.Search.Store.Backend

    def delete_by_path(_path), do: {:error, :delete_failed}

    def new(_project), do: {:ok, :new}
    def prepare(_), do: {:ok, :empty}
    def insert(_entries), do: :ok
    def replace_all(_entries), do: :ok
    def find_by_subject(_subject, _type, _subtype), do: []
    def find_by_prefix(_prefix, _type, _subtype), do: []
    def find_by_ids(_ids, _type, _subtype), do: []
    def reduce(acc, _fun), do: acc
    def siblings(_entry), do: []
    def parent(_entry), do: nil
    def structure_for_path(_path), do: {:ok, %{}}
    def drop, do: :ok
    def destroy(_state), do: :ok
  end

  describe "update_nosync/3" do
    test "catches timeout from backend and logs the warning" do
      Logger.put_module_level(State, :warning)
      on_exit(fn -> Logger.put_module_level(State, Logger.level()) end)

      project = project()

      state =
        State.new(
          project,
          fn _project, _backend -> :ok end,
          fn _project, _backend -> :ok end,
          TimeoutBackend
        )

      {result, log} =
        with_log(fn ->
          State.update_nosync(state, "/some/path.ex", [])
        end)

      assert {:ok, %State{}} = result
      assert log =~ "Timeout updating index for path: /some/path.ex"
    end
  end

  describe "refresh_index/1" do
    @tag :tmp_dir
    test "returns index update errors instead of crashing", %{tmp_dir: tmp_dir} do
      project = tmp_dir |> Forge.Document.Path.to_uri() |> Project.bare()

      state =
        State.new(
          project,
          fn _project, _backend -> :ok end,
          fn _project, _backend -> {:error, :update_failed} end,
          DeleteErrorBackend
        )

      assert {:error, :update_failed} = State.refresh_index(state)
    end
  end
end

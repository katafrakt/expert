defmodule Expert.Configuration do
  @moduledoc """
  Encapsulates expert configuration options and client capability support.
  """

  alias Expert.Configuration.Support
  alias Expert.Configuration.WorkspaceSymbols
  alias Expert.Protocol.Id
  alias GenLSP.Notifications.WorkspaceDidChangeConfiguration
  alias GenLSP.Requests
  alias GenLSP.Structures

  @default_lsp_log_level :info
  @default_file_log_level :debug

  @type lsp_level :: :error | :warning | :info | :log
  @type file_level :: :debug | :info | :warning | :error

  defstruct support: nil,
            client_name: nil,
            additional_watched_extensions: nil,
            workspace_symbols: %WorkspaceSymbols{},
            log_level: @default_lsp_log_level,
            file_log_level: @default_file_log_level,
            elixir_source_path: nil

  @type t :: %__MODULE__{
          support: support | nil,
          client_name: String.t() | nil,
          additional_watched_extensions: [String.t()] | nil,
          workspace_symbols: WorkspaceSymbols.t(),
          log_level: lsp_level(),
          file_log_level: file_level(),
          elixir_source_path: String.t() | nil
        }

  @opaque support :: Support.t()

  @spec new(Structures.ClientCapabilities.t(), String.t() | nil) :: t
  def new(%Structures.ClientCapabilities{} = client_capabilities, client_name) do
    support = Support.new(client_capabilities)

    %__MODULE__{support: support, client_name: client_name}
  end

  @spec new(keyword()) :: t
  def new(attrs \\ []) do
    struct!(__MODULE__, [support: Support.new()] ++ attrs)
  end

  @spec set(t) :: t
  def set(%__MODULE__{} = config) do
    :persistent_term.put(__MODULE__, config)
    config
  end

  @spec get() :: t
  def get do
    :persistent_term.get(__MODULE__, nil) || struct!(__MODULE__, support: Support.new())
  end

  @spec client_support(atom()) :: term()
  def client_support(key) when is_atom(key) do
    client_support(get().support, key)
  end

  @spec log_level() :: lsp_level()
  def log_level do
    get().log_level
  end

  @spec file_log_level() :: file_level()
  def file_log_level do
    get().file_log_level
  end

  @spec window_log_message_enabled?() :: boolean()
  def window_log_message_enabled? do
    case get().client_name do
      nil ->
        true

      client_name ->
        # Workaround for Eglot/Emacs behavior discussed in:
        # https://github.com/expert-lsp/expert/issues/382
        client_name
        |> String.trim()
        |> String.downcase()
        |> then(&(&1 not in ["emacs", "eglot"]))
    end
  end

  defp client_support(%Support{} = client_support, key) do
    case Map.fetch(client_support, key) do
      {:ok, value} -> value
      :error -> raise ArgumentError, "unknown key: #{inspect(key)}"
    end
  end

  @spec default() :: {:ok, t} | {:ok, t, Requests.ClientRegisterCapability.t()}
  def default do
    apply_config_change(get(), %{})
  end

  @spec on_change(WorkspaceDidChangeConfiguration.t() | :defaults) ::
          {:ok, t}
          | {:ok, t, Requests.ClientRegisterCapability.t()}
  def on_change(:defaults) do
    apply_config_change(get(), %{})
  end

  def on_change(%WorkspaceDidChangeConfiguration{} = change) do
    apply_config_change(get(), change.params.settings)
  end

  defp apply_config_change(%__MODULE__{} = old_config, %{} = settings) do
    new_config =
      old_config
      |> set_lsp_log_level(settings)
      |> set_file_log_level(settings)
      |> set_workspace_symbols(settings)
      |> set_elixir_source_path(settings)
      |> set()

    apply_file_log_level(new_config)
    maybe_watched_extensions_request(new_config, settings)
  end

  defp apply_config_change(%__MODULE__{} = old_config, _settings) do
    {:ok, old_config}
  end

  defp set_lsp_log_level(%__MODULE__{} = config, settings) do
    %__MODULE__{config | log_level: parse_lsp_log_level(settings)}
  end

  defp parse_lsp_log_level(%{"logLevel" => "error"}), do: :error
  defp parse_lsp_log_level(%{"logLevel" => "warning"}), do: :warning
  defp parse_lsp_log_level(%{"logLevel" => "info"}), do: :info
  defp parse_lsp_log_level(%{"logLevel" => "log"}), do: :log
  defp parse_lsp_log_level(_), do: @default_lsp_log_level

  defp set_file_log_level(%__MODULE__{} = config, %{"fileLogLevel" => value}) do
    %__MODULE__{config | file_log_level: parse_file_log_level(value)}
  end

  defp set_file_log_level(%__MODULE__{} = config, _settings) do
    config
  end

  defp parse_file_log_level("debug"), do: :debug
  defp parse_file_log_level("info"), do: :info
  defp parse_file_log_level("warning"), do: :warning
  defp parse_file_log_level("error"), do: :error
  defp parse_file_log_level(_), do: @default_file_log_level

  defp apply_file_log_level(%__MODULE__{file_log_level: level}) do
    handler_name = Expert.Logging.ProjectLogFile.handler_name()

    case :logger.set_handler_config(handler_name, :level, level) do
      :ok -> :ok
      {:error, _} -> :ok
    end
  end

  defp set_elixir_source_path(%__MODULE__{} = config, %{"elixirSourcePath" => value})
       when is_binary(value) do
    %__MODULE__{config | elixir_source_path: value}
  end

  defp set_elixir_source_path(%__MODULE__{} = config, %{"elixirSourcePath" => _}) do
    %__MODULE__{config | elixir_source_path: nil}
  end

  defp set_elixir_source_path(%__MODULE__{} = config, _settings) do
    config
  end

  defp set_workspace_symbols(%__MODULE__{} = config, settings) do
    %__MODULE__{config | workspace_symbols: WorkspaceSymbols.new(settings)}
  end

  defp maybe_watched_extensions_request(
         %__MODULE__{} = config,
         %{"additionalWatchedExtensions" => []}
       ) do
    {:ok, config}
  end

  defp maybe_watched_extensions_request(
         %__MODULE__{} = config,
         %{"additionalWatchedExtensions" => extensions}
       )
       when is_list(extensions) do
    register_id = Id.next()
    request_id = Id.next()

    watchers = Enum.map(extensions, fn ext -> %{"globPattern" => "**/*#{ext}"} end)

    registration =
      %Structures.Registration{
        id: request_id,
        method: "workspace/didChangeWatchedFiles",
        register_options: %{"watchers" => watchers}
      }

    request = %Requests.ClientRegisterCapability{
      id: register_id,
      params: %Structures.RegistrationParams{registrations: [registration]}
    }

    {:ok, config, request}
  end

  defp maybe_watched_extensions_request(%__MODULE__{} = config, _settings) do
    {:ok, config}
  end
end

defmodule Expert.Search.IndexerTest do
  use ExUnit.Case
  use Patch

  import Forge.Test.Fixtures

  alias Expert.EngineApi
  alias Expert.Search.Indexer
  alias Expert.Search.Indexer.Beams
  alias Expert.Search.Indexer.Manifest
  alias Expert.Search.Indexer.Manifest.Entry, as: ManifestEntry
  alias Expert.Search.Indexer.ManifestStore
  alias Expert.Search.Indexer.ModuleRegistry
  alias Expert.Search.Indexer.Paths
  alias Expert.Search.Indexer.Source
  alias Expert.Search.Store
  alias Forge.Project
  alias Forge.Search.Indexer.Entry

  defmodule FakeBackend do
    @entries_key {__MODULE__, :entries}
    @calls_key {__MODULE__, :calls}

    def set_entries(entries) when is_list(entries) do
      :persistent_term.put(@entries_key, entries)
    end

    def entries do
      :persistent_term.get(@entries_key, [])
    end

    def path_to_ids do
      Enum.reduce(entries(), %{}, fn
        %Entry{path: path} = entry, path_to_ids when is_integer(entry.id) ->
          Map.update(path_to_ids, path, entry.id, &max(&1, entry.id))

        _entry, path_to_ids ->
          path_to_ids
      end)
    end

    def reset_calls, do: :persistent_term.put(@calls_key, [])
    def calls, do: @calls_key |> :persistent_term.get([]) |> Enum.reverse()

    def replace(entries) do
      record_call({:replace, entries})
      set_entries(entries)
      :ok
    end

    def insert(entries) do
      record_call({:insert, entries})
      set_entries(entries() ++ entries)
      :ok
    end

    def apply_index_update(entries, paths_to_clear) do
      record_call({:apply_index_update, entries, paths_to_clear})

      paths_to_clear =
        entries
        |> Enum.map(& &1.path)
        |> Kernel.++(paths_to_clear)
        |> MapSet.new()

      kept_entries =
        Enum.reject(entries(), &MapSet.member?(paths_to_clear, &1.path))

      set_entries(kept_entries ++ entries)
      :ok
    end

    def record_call(call) do
      :persistent_term.put(@calls_key, [call | :persistent_term.get(@calls_key, [])])
    end
  end

  setup do
    project = project()
    start_supervised!({Expert.Project.Store, []})
    start_supervised!({ModuleRegistry, project})
    start_supervised!(Engine.ApplicationCache)

    patch(Engine.Api.Proxy, :broadcast, fn _ -> :ok end)

    patch(Expert.Progress, :begin, fn _title, _opts ->
      {:ok, System.unique_integer([:positive])}
    end)

    patch(Expert.Progress, :report, :ok)
    patch(Expert.Progress, :complete, :ok)

    patch(EngineApi, :project_configuration, fn _engine_project, configured_project ->
      Engine.Mix.project_configuration(configured_project)
    end)

    patch(EngineApi, :call, fn
      _project, Engine.ApplicationCache, :clear, [] ->
        Engine.ApplicationCache.clear()

      _project, Engine.ApplicationCache, :application, [module] ->
        Engine.ApplicationCache.application(module)

      _project, Engine.ApplicationCache, :available_module?, [module] ->
        Engine.ApplicationCache.available_module?(module)

      _project, Engine.Modules, :exunit_module?, [module] ->
        Engine.Modules.exunit_module?(module)

      _project, Engine.Modules, :exports, [module] ->
        Engine.Modules.exports(module)
    end)

    FakeBackend.set_entries([])
    FakeBackend.reset_calls()

    patch(Store, :replace, fn _project, entries -> FakeBackend.replace(entries) end)
    patch(Store, :insert, fn _project, entries -> FakeBackend.insert(entries) end)

    patch(Store, :apply_index_update, fn _project, entries, paths_to_clear ->
      FakeBackend.apply_index_update(entries, paths_to_clear)
    end)

    patch(Store, :path_to_ids, fn _project -> FakeBackend.path_to_ids() end)

    ManifestStore.invalidate(project)
    {:ok, project: project}
  end

  defp create_index(project) do
    FakeBackend.reset_calls()
    assert :ok = Indexer.create_index(project)
    inserted_entries(FakeBackend.calls())
  end

  defp update_index(project) do
    FakeBackend.reset_calls()
    assert :ok = Indexer.update_index(project)
    calls = FakeBackend.calls()
    entries = inserted_entries(calls)
    paths_to_clear = cleared_paths(calls)
    assert Enum.uniq(paths_to_clear) == paths_to_clear
    {entries, paths_to_clear}
  end

  defp inserted_entries(calls) do
    Enum.flat_map(calls, fn
      {:insert, entries} -> entries
      {:apply_index_update, entries, _paths} -> entries
      _call -> []
    end)
  end

  defp start_registry(project) do
    start_supervised!({ModuleRegistry, project})
    Expert.Project.Store.add_projects([project])
    Expert.Project.Store.transition(project, :ready)
  end

  defp cleared_paths(calls) do
    Enum.flat_map(calls, fn
      {:apply_index_update, _entries, paths} -> paths
      _call -> []
    end)
  end

  defp index_beams(paths) do
    paths
    |> Beams.stream()
    |> Enum.reduce({[], []}, fn
      {nil, manifest_entries}, {entries, manifests} ->
        {entries, [manifest_entries | manifests]}

      {entry, manifest_entries}, {entries, manifests} ->
        {[entry | entries], [manifest_entries | manifests]}
    end)
    |> then(fn {entries, manifests} ->
      {Enum.reverse(entries), manifests |> Enum.reverse() |> List.flatten()}
    end)
  end

  defp write_file!(path, contents) do
    path = Forge.Path.native(path)

    File.mkdir_p!(Path.dirname(path))
    File.write!(path, contents)
    path
  end

  defp write_mix_project!(root, module_name, project_config) do
    write_file!(Path.join(root, "mix.exs"), """
    defmodule #{module_name} do
      use Mix.Project

      def project do
        #{project_config}
      end)
    end
    """)
  end

  defp mix_build_file!(root, relative_path, config) do
    build_root =
      File.cd!(root, fn ->
        config
        |> Keyword.put_new(:build_per_environment, true)
        |> Mix.Project.build_path()
        |> Path.dirname()
      end)

    [build_root | List.wrap(relative_path)]
    |> Path.join()
    |> Forge.Path.native()
  end

  describe "create_index/1" do
    test "clears Engine application metadata before and after indexing", %{project: project} do
      test_pid = self()

      patch(EngineApi, :clear_application_cache, fn ^project ->
        send(test_pid, :application_cache_cleared)
        :ok
      end)

      assert :ok = Indexer.create_index(project, paths: %Paths{})

      assert_receive :application_cache_cleared
      assert_receive :application_cache_cleared
    end

    @tag :tmp_dir
    test "builds the module registry before source indexing", %{
      project: project,
      tmp_dir: tmp_dir
    } do
      test_pid = self()
      module = Module.concat(BeamDependencyIndexerTest, :CatalogImported)

      dep_source = """
      defmodule #{inspect(module)} do
        def imported_function, do: :ok
        defmacro imported_macro, do: :ok
      end
      """

      %{beam_path: beam_path} =
        with_beam_dependency(tmp_dir,
          module: module,
          modules: [module],
          dep_source: dep_source,
          rewrite_source?: false
        )

      :code.purge(module)
      :code.delete(module)

      source_path =
        write_file!(Path.join(tmp_dir, "registry_consumer.ex"), """
        defmodule RegistryConsumer do
          import #{inspect(module)}

          def run do
            imported_function()
            imported_macro()
          end
        end
        """)

      paths = %Paths{
        source_paths: [source_path],
        beam_paths: [beam_path],
        applications: %{Path.dirname(beam_path) => :registry_dependency}
      }

      patch(Source, :index, fn path, source, extractors, project ->
        send(test_pid, {:registry_application, ModuleRegistry.application(project, module)})
        real(Source).index(path, source, extractors, project)
      end)

      assert :ok =
               Indexer.create_index(project, paths: paths)

      assert_receive {:registry_application, :registry_dependency}

      imported_subjects = [
        Forge.Formats.mfa(module, :imported_function, 0),
        Forge.Formats.mfa(module, :imported_macro, 0)
      ]

      references =
        Enum.filter(FakeBackend.entries(), fn entry ->
          entry.subject in imported_subjects and entry.subtype == :reference
        end)

      assert Enum.sort(Enum.map(references, & &1.subject)) == Enum.sort(imported_subjects)
      assert Enum.all?(references, &(&1.application == :registry_dependency))
      refute Code.loaded?(module)
    end

    @tag :tmp_dir
    test "source entries override matching BEAM entries", %{project: project, tmp_dir: tmp_dir} do
      %{beam_path: beam_path, dep_file: source_path, module: module} =
        with_beam_dependency(tmp_dir, rewrite_source?: false)

      paths = %Paths{
        source_paths: [source_path],
        beam_paths: [beam_path],
        applications: %{Path.dirname(beam_path) => :source_dependency}
      }

      assert :ok =
               Indexer.create_index(project, paths: paths)

      function_subject = Forge.Formats.mfa(module, :public_fun, 0)

      assert [%Entry{path: ^source_path}] =
               Enum.filter(FakeBackend.entries(), fn entry ->
                 entry.subject == function_subject and entry.subtype == :definition
               end)
    end

    test "persists produced entries", %{project: project} do
      entry_stream = create_index(project)
      entries = Enum.to_list(entry_stream)
      project_root = Project.root_path(project)

      assert not Enum.empty?(entries)
      assert Enum.all?(entries, &Forge.Path.contains?(&1.path, project_root))
      assert Enum.all?(entries, &(&1.path == Forge.Path.native(&1.path)))
    end

    @tag :tmp_dir
    test "emits bounded chunks and keeps completed chunks after a write error", %{
      project: project,
      tmp_dir: tmp_dir
    } do
      path = write_file!(Path.join(tmp_dir, "large.ex"), "defmodule Large do\nend")

      entries =
        for id <- 1..4_001 do
          %Entry{
            id: id,
            path: path,
            subject: "Large.function_#{id}/0",
            type: {:function, :public},
            subtype: :definition,
            block_id: :root
          }
        end

      patch(Source, :index, fn ^path, _source, nil, ^project ->
        {:ok, entries}
      end)

      patch(Store, :insert, fn _project, chunk ->
        if length(chunk) == 1 do
          FakeBackend.record_call({:insert, chunk})
          {:error, :disk_full}
        else
          FakeBackend.insert(chunk)
        end
      end)

      assert {:error, {:store, :disk_full}} =
               Indexer.create_index(project, paths: %Paths{source_paths: [path]})

      registry = ModuleRegistry.name(project)
      assert :ets.info(registry, :owner) == Process.whereis(ModuleRegistry.name(project))

      [{:replace, []}, {:insert, first_chunk}, {:insert, final_chunk}] = FakeBackend.calls()
      assert length(first_chunk) == 4_000
      assert [_entry] = final_chunk
      assert length(FakeBackend.entries()) == 4_000
      assert :missing = ManifestStore.load(project)
    end

    test "does not commit a manifest when resetting the Store fails", %{project: project} do
      test_pid = self()

      patch(Store, :replace, fn ^project, [] -> {:error, :replace_failed} end)

      patch(ManifestStore, :commit, fn ^project, _manifest ->
        send(test_pid, :manifest_committed)
        :ok
      end)

      assert {:error, {:store, :replace_failed}} =
               Indexer.create_index(project, paths: %Paths{})

      refute_receive :manifest_committed
      assert :missing = ManifestStore.load(project)
    end

    test "entries are either .ex or .exs files", %{project: project} do
      entries = create_index(project)
      assert Enum.all?(entries, fn entry -> Path.extname(entry.path) in [".ex", ".exs"] end)
    end

    test "indexes bare projects without treating root/deps as a dependency directory" do
      bare_root = native_join([fixtures_path(), "scratch"])
      bare_project = bare_root |> Forge.Document.Path.to_uri() |> Project.bare()

      patch(Engine, :get_project, fn -> bare_project end)
      start_registry(bare_project)

      entries = create_index(bare_project)
      assert Enum.any?(entries, &(&1.path == native_join([bare_root, "bare_file.ex"])))
    end

    @tag :tmp_dir
    test "indexes active path dependency beams", %{tmp_dir: tmp_dir} do
      %{module: module, project: project} = with_beam_dependency(tmp_dir)
      start_registry(project)

      entries = create_index(project)

      assert Enum.any?(entries, &(&1.subject == module and &1.subtype == :definition))

      assert Enum.any?(
               entries,
               &(&1.subject == Forge.Formats.mfa(module, :public_fun, 0) and
                   &1.subtype == :definition)
             )

      refute Enum.any?(entries, &(&1.subject == Forge.Formats.mfa(module, :private_fun, 0)))
    end

    @tag :tmp_dir
    test "does not index dependency beams when the dependency has app false", %{tmp_dir: tmp_dir} do
      %{module: module, project: project} =
        with_beam_dependency(tmp_dir, dep_opts: [path: "deps/beam_dep", app: false])

      start_registry(project)
      entries = create_index(project)

      refute Enum.any?(entries, &(&1.subject == module and &1.subtype == :definition))
    end

    @tag :tmp_dir
    test "indexes protocol callback definitions from beam metadata", %{tmp_dir: tmp_dir} do
      %{module: protocol, project: project} = with_protocol_beam_dependency(tmp_dir)
      start_registry(project)

      entries = create_index(project)

      assert Enum.any?(entries, &(&1.subject == protocol and &1.type == {:protocol, :definition}))

      assert Enum.any?(
               entries,
               &(&1.subject == Forge.Formats.mfa(protocol, :run, 1) and
                   &1.type == {:function, :public} and &1.subtype == :definition)
             )

      refute Enum.any?(entries, &(&1.subject == Forge.Formats.mfa(protocol, :impl_for, 1)))
      refute Enum.any?(entries, &(&1.subject == Forge.Formats.mfa(protocol, :impl_for!, 1)))
      refute Enum.any?(entries, &(&1.subject == Forge.Formats.mfa(protocol, :__protocol__, 1)))
    end

    @tag :tmp_dir
    test "indexes protocol implementations from beam metadata", %{tmp_dir: tmp_dir} do
      %{impl_module: impl_module, project: project, protocol: protocol} =
        with_protocol_implementation_beam_dependency(tmp_dir)

      start_registry(project)
      entries = create_index(project)

      assert Enum.any?(
               entries,
               &(&1.subject == protocol and &1.type == {:protocol, :implementation} and
                   &1.subtype == :definition)
             )

      assert Enum.any?(
               entries,
               &(&1.subject == impl_module and &1.type == :module and &1.subtype == :definition)
             )

      refute Enum.any?(
               entries,
               &(&1.subject == impl_module and &1.type == {:protocol, :implementation})
             )
    end

    @tag :tmp_dir
    test "locates protocol implementation definitions on defimpl", %{tmp_dir: tmp_dir} do
      %{dep_file: dep_file, project: project, protocol: protocol} =
        with_protocol_implementation_beam_dependency(tmp_dir, rewrite_source?: false)

      start_registry(project)
      line = line_containing(dep_file, "defimpl")
      expected_column = expected_column(dep_file, "defimpl")
      expected_length = line |> String.trim() |> String.length()

      entries = create_index(project)

      entry =
        Enum.find(entries, &(&1.subject == protocol and &1.type == {:protocol, :implementation}))

      assert %Entry{range: range, subtype: :definition} = entry
      assert range.start.character == expected_column
      assert range.end.character == expected_column + expected_length
    end

    @tag :tmp_dir
    test "indexes entries from transitive dependency beams", %{tmp_dir: tmp_dir} do
      %{module: module, project: project} = with_transitive_beam_dependency(tmp_dir)
      start_registry(project)

      entries = create_index(project)
      assert Enum.any?(entries, &(&1.subject == module and &1.subtype == :definition))
    end

    @tag :tmp_dir
    test "locates beam module definitions on the module name", %{tmp_dir: tmp_dir} do
      %{dep_file: dep_file, module: module, project: project} =
        with_beam_dependency(tmp_dir, rewrite_source?: false)

      start_registry(project)
      module_name = inspect(module)
      expected_column = expected_column(dep_file, module_name)

      entries = create_index(project)
      entry = Enum.find(entries, &(&1.subject == module and &1.type == :module))

      assert %Entry{range: range, subtype: :definition} = entry
      assert range.start.character == expected_column
      assert range.end.character == expected_column + String.length(module_name)
    end

    @tag :tmp_dir
    test "locates nested beam module definitions on the nested module name", %{tmp_dir: tmp_dir} do
      %{dep_file: dep_file, module: module, project: project} =
        with_nested_beam_dependency(tmp_dir)

      start_registry(project)
      expected_column = expected_column(dep_file, "Inner")

      entries = create_index(project)
      entry = Enum.find(entries, &(&1.subject == module and &1.type == :module))

      assert %Entry{range: range, subtype: :definition} = entry
      assert range.start.character == expected_column
      assert range.end.character == expected_column + String.length("Inner")
    end

    @tag :tmp_dir
    test "caches dependency beams with no debug metadata", %{tmp_dir: tmp_dir} do
      %{beam_path: beam_path, module: module, project: project} =
        with_beam_dependency(tmp_dir, debug_info?: false, rewrite_source?: false)

      start_registry(project)
      assert {entries, _cleared_paths} = update_index(project)
      assert {:ok, manifest} = ManifestStore.load(project)

      refute Enum.any?(entries, &(&1.subject == module))

      assert {:ok, %ManifestEntry{kind: :beam, output_path: nil}} =
               Manifest.fetch(manifest, beam_path)
    end

    @tag :tmp_dir
    test "uses BEAM metadata for public dependency definitions", %{tmp_dir: tmp_dir} do
      %{beam_path: beam_path, dep_file: dep_file, module: module, project: project} =
        with_beam_dependency(tmp_dir, rewrite_source?: false)

      start_registry(project)
      File.touch!(dep_file, {{2100, 1, 1}, {0, 0, 0}})
      spy(Source)

      assert {entries, _cleared_paths} = update_index(project)
      assert {:ok, manifest} = ManifestStore.load(project)

      assert Enum.any?(entries, &(&1.subject == module and &1.subtype == :definition))

      assert Enum.any?(entries, &(&1.subject == Forge.Formats.mfa(module, :public_fun, 0)))
      refute Enum.any?(entries, &(&1.subject == Forge.Formats.mfa(module, :private_fun, 0)))
      refute_called(Source.index(^dep_file, _, _, _))

      assert {:ok, %ManifestEntry{kind: :beam, output_path: ^dep_file, source_path: ^dep_file}} =
               Manifest.fetch(manifest, beam_path)

      FakeBackend.set_entries(entries)
      assert {[], []} = update_index(project)
    end

    @tag :tmp_dir
    test "does not reindex unchanged skipped dependency beams after caching them", %{
      tmp_dir: tmp_dir
    } do
      %{beam_path: beam_path, project: project} =
        with_beam_dependency(tmp_dir, debug_info?: false, rewrite_source?: false)

      start_registry(project)
      assert {entries, _cleared_paths} = update_index(project)
      FakeBackend.set_entries(entries)
      assert {:ok, manifest} = ManifestStore.load(project)

      old_manifest_entries =
        manifest
        |> Manifest.entries()
        |> Enum.reject(&(&1.input_path == beam_path))

      assert :ok = ManifestStore.commit(project, Manifest.new(old_manifest_entries))

      test_pid = self()

      patch(Expert.Progress, :begin, fn
        "Indexing dependencies metadata", _opts ->
          send(test_pid, :dependency_progress_begin)
          {:ok, System.unique_integer([:positive])}

        _title, _opts ->
          {:ok, System.unique_integer([:positive])}
      end)

      assert {entries, _cleared_paths} = update_index(project)
      assert [] = entries
      assert_receive :dependency_progress_begin
      refute_receive :dependency_progress_begin, 0

      assert {entries, _cleared_paths} = update_index(project)
      assert [] = entries
      refute_receive :dependency_progress_begin
    end

    @tag :tmp_dir
    test "clears entries when beam metadata disappears", %{tmp_dir: tmp_dir} do
      %{beam_path: beam_path, dep_file: dep_file, project: project} =
        with_beam_dependency(tmp_dir)

      start_registry(project)
      entries = create_index(project)
      FakeBackend.set_entries(entries)

      File.rm!(beam_path)

      assert {_entries, paths_to_clear} = update_index(project)
      assert dep_file in paths_to_clear
    end

    @tag :tmp_dir
    test "retains compiled definitions when newer source cannot be parsed", %{tmp_dir: tmp_dir} do
      %{dep_file: dep_file, module: module, project: project} = with_beam_dependency(tmp_dir)
      start_registry(project)

      entries = create_index(project)
      FakeBackend.set_entries(entries)

      File.touch!(dep_file, {{2100, 1, 1}, {0, 0, 0}})

      assert {updated_entries, _cleared_paths} = update_index(project)
      assert Enum.any?(updated_entries, &(&1.subject == module and &1.subtype == :definition))

      assert Enum.any?(
               updated_entries,
               &(&1.subject == Forge.Formats.mfa(module, :public_fun, 0))
             )

      refute Enum.any?(updated_entries, &(&1.path == dep_file and &1.subtype == :reference))
    end

    @tag :tmp_dir
    test "refreshes dependency definitions after recompilation", %{tmp_dir: tmp_dir} do
      %{beam_path: beam_path, dep_file: dep_file, module: module, project: project} =
        with_beam_dependency(tmp_dir, rewrite_source?: false)

      start_registry(project)
      FakeBackend.set_entries(create_index(project))

      File.write!(dep_file, """
      defmodule #{inspect(module)} do
        def newly_compiled_function, do: :updated
      end
      """)

      File.touch!(dep_file, {{2100, 1, 1}, {0, 0, 0}})

      assert {before_compile, _cleared_paths} = update_index(project)
      assert Enum.any?(before_compile, &(&1.subject == Forge.Formats.mfa(module, :public_fun, 0)))

      refute Enum.any?(
               before_compile,
               &(&1.subject == Forge.Formats.mfa(module, :newly_compiled_function, 0))
             )

      FakeBackend.set_entries(before_compile)
      Code.compiler_options(ignore_module_conflict: true)
      assert [^module] = compile_to_path!([dep_file], Path.dirname(beam_path))
      File.touch!(beam_path, {{2101, 1, 1}, {0, 0, 0}})

      assert {after_compile, _cleared_paths} = update_index(project)

      assert Enum.any?(
               after_compile,
               &(&1.subject == Forge.Formats.mfa(module, :newly_compiled_function, 0))
             )

      refute Enum.any?(after_compile, &(&1.subject == Forge.Formats.mfa(module, :public_fun, 0)))
    end

    @tag :tmp_dir
    test "retains source entries when only a project beam changes", %{tmp_dir: tmp_dir} do
      %{beam_path: beam_path, dep_file: source_path, module: module, project: project} =
        with_beam_dependency(tmp_dir, rewrite_source?: false)

      start_registry(project)

      patch(Paths, :for_project, fn ^project ->
        %Paths{
          source_paths: [source_path],
          beam_paths: [beam_path],
          applications: %{Path.dirname(beam_path) => :beam_dep}
        }
      end)

      FakeBackend.set_entries(create_index(project))
      private_subject = Forge.Formats.mfa(module, :private_fun, 0)

      assert Enum.any?(FakeBackend.entries(), fn entry ->
               entry.subject == private_subject and entry.subtype == :definition
             end)

      assert Enum.any?(FakeBackend.entries(), fn entry ->
               entry.subject == private_subject and entry.subtype == :reference
             end)

      File.touch!(beam_path, {{2101, 1, 1}, {0, 0, 0}})
      update_index(project)

      assert Enum.any?(FakeBackend.entries(), fn entry ->
               entry.subject == private_subject and entry.subtype == :definition
             end)

      assert Enum.any?(FakeBackend.entries(), fn entry ->
               entry.subject == private_subject and entry.subtype == :reference
             end)
    end

    @tag :tmp_dir
    test "clears entries when a dependency is removed but its beam remains", %{tmp_dir: tmp_dir} do
      %{app_root: app_root, beam_path: beam_path, dep_file: dep_file, project: project} =
        with_beam_dependency(tmp_dir)

      start_registry(project)
      entries = create_index(project)
      FakeBackend.set_entries(entries)

      project = project_without_beam_dependency(app_root)

      assert File.exists?(beam_path)
      assert {_entries, paths_to_clear} = update_index(project)
      assert dep_file in paths_to_clear
    end

    @tag :tmp_dir
    test "retains entries from remaining beams for the same source", %{tmp_dir: tmp_dir} do
      %{
        beam_paths: [removed_beam_path, kept_beam_path],
        modules: [removed_module, kept_module],
        project: project
      } = with_beam_dependency(tmp_dir, module_count: 2)

      start_registry(project)
      entries = create_index(project)
      FakeBackend.set_entries(entries)

      File.rm!(removed_beam_path)

      assert {updated_entries, _cleared_paths} = update_index(project)

      assert Enum.any?(
               updated_entries,
               &(&1.subject == kept_module and &1.subtype == :definition)
             )

      refute Enum.any?(updated_entries, &(&1.subject == removed_module))

      assert File.exists?(kept_beam_path)
      refute File.exists?(removed_beam_path)
    end
  end

  describe "update_index/1 persistence" do
    @tag :tmp_dir
    test "applies a replacement in one update", %{project: project, tmp_dir: tmp_dir} do
      path = write_file!(Path.join(tmp_dir, "large.ex"), "defmodule Large do\nend")
      {:ok, manifest_entry} = ManifestEntry.source(path)
      assert :ok = ManifestStore.commit(project, Manifest.new([manifest_entry]))
      File.touch!(path, {{2100, 1, 1}, {0, 0, 0}})

      entries =
        for id <- 1..4_001 do
          %Entry{
            id: id,
            path: path,
            subject: "Large.function_#{id}/0",
            type: {:function, :public},
            subtype: :definition,
            block_id: :root
          }
        end

      patch(Source, :index, fn ^path, _source, nil, ^project ->
        {:ok, entries}
      end)

      FakeBackend.set_entries([%Entry{id: 4_002, path: path}])
      FakeBackend.reset_calls()

      assert :ok =
               Indexer.update_index(project, paths: %Paths{source_paths: [path]})

      [{:apply_index_update, updated_entries, []}] = FakeBackend.calls()
      assert length(updated_entries) == 4_001
    end

    @tag :tmp_dir
    test "keeps stored entries when an update fails", %{project: project, tmp_dir: tmp_dir} do
      path = write_file!(Path.join(tmp_dir, "changed.ex"), "defmodule Changed do\nend")
      {:ok, manifest_entry} = ManifestEntry.source(path)
      assert :ok = ManifestStore.commit(project, Manifest.new([manifest_entry]))
      File.touch!(path, {{2100, 1, 1}, {0, 0, 0}})

      old_entry = %Entry{
        id: 1,
        path: path,
        subject: Old,
        type: :module,
        subtype: :definition,
        block_id: :root
      }

      FakeBackend.set_entries([old_entry])

      patch(Store, :apply_index_update, fn ^project, [_ | _], [] ->
        {:error, :disk_full}
      end)

      assert {:error, {:store, :disk_full}} =
               Indexer.update_index(project, paths: %Paths{source_paths: [path]})

      assert [^old_entry] = FakeBackend.entries()
      assert :missing = ManifestStore.load(project)
    end
  end

  describe "update_index/1 with dependency beams" do
    test "reindexes beam siblings sharing source" do
      tmp_dir = Path.join(System.tmp_dir!(), "indexer-#{unique_id()}")

      on_exit(fn -> File.rm_rf!(tmp_dir) end)

      parent = Module.concat(BeamDependencyIndexerTest, :SiblingParent)
      child = Module.concat(parent, :Child)

      dep_source = """
      defmodule #{inspect(parent)} do
        def parent_fun, do: :ok

        defmodule Child do
          def child_fun, do: :ok
        end
      end
      """

      %{beam_paths: beam_paths, project: project} =
        with_beam_dependency(tmp_dir,
          module: parent,
          modules: [parent, child],
          expected_modules: [parent, child],
          rewrite_source?: false,
          dep_source: dep_source
        )

      start_registry(project)

      parent_beam_path =
        Enum.find(beam_paths, &String.ends_with?(&1, Atom.to_string(parent) <> ".beam"))

      child_beam_path =
        Enum.find(beam_paths, &String.ends_with?(&1, Atom.to_string(child) <> ".beam"))

      assert is_binary(parent_beam_path)
      assert is_binary(child_beam_path)

      {parent_entries, parent_manifest_entries} = index_beams([parent_beam_path])
      FakeBackend.set_entries(parent_entries)
      assert :ok = ManifestStore.commit(project, Manifest.new(parent_manifest_entries))

      assert {updated_entries, _cleared_paths} = update_index(project)
      updated_entries = Enum.to_list(updated_entries)

      assert Enum.any?(updated_entries, &(&1.subject == parent and &1.subtype == :definition))
      assert Enum.any?(updated_entries, &(&1.subject == child and &1.subtype == :definition))
    end
  end

  @ephemeral_file_name "ephemeral.ex"

  def with_an_ephemeral_file(%{project: project}, file_contents) do
    file_path = native_join([Project.root_path(project), "lib", @ephemeral_file_name])
    File.write!(file_path, file_contents)

    on_exit(fn ->
      File.rm(file_path)
    end)

    {:ok, file_path: file_path}
  end

  def with_a_file_with_a_module(context) do
    file_contents = ~s[
        defmodule Ephemeral do
        end
      ]

    with_an_ephemeral_file(context, file_contents)
  end

  def with_an_existing_index(%{project: project}) do
    entries = create_index(project)
    FakeBackend.set_entries(entries)

    {:ok, entries: entries}
  end

  describe "update_index/1 removes paths that became non-indexable" do
    @tag :tmp_dir
    test "deletes previously indexed configured build files even when they still exist", %{
      tmp_dir: tmp_dir
    } do
      source_file = native_join([tmp_dir, "lib", "source_file.ex"])
      build_file = mix_build_file!(tmp_dir, "stale.ex", build_path: "custom_build")

      write_mix_project!(
        tmp_dir,
        "StaleConfiguredBuildPathIndexerTest.MixProject",
        ~s([app: :stale_configured_build_path_indexer_test, version: "0.1.0", build_path: "custom_build"])
      )

      write_file!(source_file, "defmodule SourceFile do end")
      write_file!(build_file, "defmodule StaleBuildFile do end")

      project = tmp_dir |> Forge.Document.Path.to_uri() |> Project.new()
      start_registry(project)
      patch(Paths, :for_project, fn ^project -> %Paths{source_paths: [source_file]} end)

      FakeBackend.set_entries([%Entry{id: 1, path: build_file}])
      ManifestStore.invalidate(project)

      assert {_updated_entries, cleared_paths} = update_index(project)
      assert build_file in cleared_paths
      refute Enum.any?(FakeBackend.entries(), &(&1.path == build_file))
    end
  end

  describe "update_index/1 manifest commits" do
    @tag :tmp_dir
    test "keeps the manifest invalid if committing a refresh fails", %{tmp_dir: tmp_dir} do
      source_file = native_join([tmp_dir, "lib", "source_file.ex"])
      write_file!(source_file, "defmodule SourceFile do end")

      project = tmp_dir |> Forge.Document.Path.to_uri() |> Project.bare()

      start_registry(project)
      assert {entries, _cleared_paths} = update_index(project)
      assert [_ | _] = entries
      assert {:ok, _old_manifest} = ManifestStore.load(project)

      write_file!(source_file, "defmodule ChangedSourceFile do end")
      File.touch!(source_file, {{2100, 1, 1}, {0, 0, 0}})

      patch(ManifestStore, :commit, fn ^project, _manifest ->
        {:error, :commit_failed}
      end)

      assert {:error, :commit_failed} = Indexer.update_index(project)
      assert :missing = ManifestStore.load(project)
    end
  end

  describe "update_index/1 encounters a new file" do
    setup [:with_an_existing_index, :with_a_file_with_a_module]

    test "the ephemeral file is not previously present in the index", %{entries: entries} do
      refute Enum.any?(entries, fn entry -> Path.basename(entry.path) == @ephemeral_file_name end)
    end

    test "the ephemeral file is listed in the updated index", %{project: project} do
      assert {entries, _cleared_paths} = update_index(project)
      assert [_structure, updated_entry] = entries

      assert Path.basename(updated_entry.path) == @ephemeral_file_name
      assert updated_entry.subject == Ephemeral
    end

    test "writes returned entries into the backend", %{project: project} do
      assert {entries, _cleared_paths} = update_index(project)
      assert [_structure, updated_entry] = entries

      assert Enum.any?(FakeBackend.entries(), &(&1.subject == updated_entry.subject))
    end

    test "reindexes a manifest output missing from the backend", %{
      project: project,
      file_path: file_path
    } do
      FakeBackend.set_entries(Enum.reject(FakeBackend.entries(), &(&1.path == file_path)))

      assert {entries, _cleared_paths} = update_index(project)
      assert [_structure, updated_entry] = entries

      assert updated_entry.path == file_path
      assert updated_entry.subject == Ephemeral
    end
  end

  def with_an_ephemeral_empty_file(context) do
    with_an_ephemeral_file(context, "")
  end

  describe "update_index/1 encounters a zero-length file" do
    setup [:with_an_existing_index, :with_an_ephemeral_empty_file]

    test "and does nothing", %{project: project} do
      assert {[], []} = update_index(project)
    end

    test "there is no progress", %{project: project} do
      assert {[], []} = update_index(project)
      refute_receive _
    end
  end

  describe "update_index/1" do
    setup [:with_a_file_with_a_module, :with_an_existing_index]

    test "sees the ephemeral file", %{entries: entries} do
      assert Enum.any?(entries, fn entry -> Path.basename(entry.path) == @ephemeral_file_name end)
    end

    test "returns the file paths of deleted files", %{project: project, file_path: file_path} do
      File.rm(file_path)

      assert {[], [^file_path]} = update_index(project)
    end

    test "updates files that have changed since the last index", %{
      project: project,
      file_path: file_path
    } do
      new_contents = ~s[
        defmodule Brand.Spanking.New do
        end
      ]

      File.write!(file_path, new_contents)
      File.touch!(file_path, {{2100, 1, 1}, {0, 0, 0}})

      assert {entries, _cleared_paths} = update_index(project)
      assert [_structure, entry] = entries

      assert entry.path == file_path
      assert entry.subject == Brand.Spanking.New
    end

    test "clears files that now index to no entries", %{project: project, file_path: file_path} do
      File.write!(file_path, "")
      File.touch!(file_path, {{2100, 1, 1}, {0, 0, 0}})

      assert {[], [^file_path]} = update_index(project)
    end
  end

  defp with_beam_dependency(tmp_dir, opts \\ []) do
    module_count = Keyword.get(opts, :module_count, 1)

    modules =
      Keyword.get_lazy(opts, :modules, fn ->
        for _ <- 1..module_count do
          Module.concat(BeamDependencyIndexerTest, :"Dep#{unique_id()}")
        end
      end)

    dep_source = Keyword.get_lazy(opts, :dep_source, fn -> default_dep_source(modules) end)

    with_compiled_beam_dependency(tmp_dir, modules, dep_source, opts)
  end

  defp default_dep_source(modules) do
    Enum.map_join(modules, "\n", fn module ->
      """
      defmodule #{inspect(module)} do
        def public_fun, do: private_fun()
        defp private_fun, do: :ok
      end
      """
    end)
  end

  defp with_nested_beam_dependency(tmp_dir) do
    outer = Module.concat(BeamDependencyIndexerTest, :"Outer#{unique_id()}")
    module = Module.concat(outer, Inner)

    dep_source = """
    defmodule #{inspect(outer)} do
      defmodule Inner do
        def public_fun, do: :ok
      end
    end
    """

    with_compiled_beam_dependency(tmp_dir, [outer, module], dep_source,
      module: module,
      rewrite_source?: false
    )
  end

  defp with_protocol_beam_dependency(tmp_dir) do
    protocol = Module.concat(BeamDependencyIndexerTest, :"Protocol#{unique_id()}")

    dep_source = """
    defprotocol #{inspect(protocol)} do
      def run(term)
    end
    """

    with_compiled_beam_dependency(tmp_dir, [protocol], dep_source, module: protocol)
  end

  defp with_protocol_implementation_beam_dependency(tmp_dir, opts \\ []) do
    protocol = Module.concat(BeamDependencyIndexerTest, :"ImplProtocol#{unique_id()}")
    target = Module.concat(BeamDependencyIndexerTest, :"ImplTarget#{unique_id()}")
    impl_module = Module.concat(protocol, target)

    dep_source = """
    defprotocol #{inspect(protocol)} do
      def run(term)
    end

    defmodule #{inspect(target)} do
      defstruct []
    end

    defimpl #{inspect(protocol)}, for: #{inspect(target)} do
      def run(term), do: term
    end
    """

    tmp_dir
    |> with_compiled_beam_dependency(
      [protocol, target, impl_module],
      dep_source,
      Keyword.put(opts, :module, impl_module)
    )
    |> Map.merge(%{impl_module: impl_module, protocol: protocol, target: target})
  end

  defp project_without_beam_dependency(app_root) do
    project_module = Module.concat(BeamDependencyIndexerTest, :"MixProject#{unique_id()}")

    File.write!(Path.join(app_root, "mix.exs"), """
    defmodule #{inspect(project_module)} do
      use Mix.Project

      def project do
        [app: :beam_dependency_indexer_test, version: "0.1.0"]
      end
    end
    """)

    File.touch!(Path.join(app_root, "mix.exs"), {{2000, 1, 1}, {0, 0, 0}})

    Module.create(
      project_module,
      quote do
        def project do
          [app: :beam_dependency_indexer_test, version: "0.1.0"]
        end
      end,
      Macro.Env.location(__ENV__)
    )

    app_root
    |> Forge.Document.Path.to_uri()
    |> Project.new()
    |> Project.set_project_module(project_module)
  end

  defp with_compiled_beam_dependency(tmp_dir, modules, dep_source, opts) do
    app_root = Path.join(tmp_dir, "beam_app")
    module = Keyword.get(opts, :module, hd(modules))

    project_module = Module.concat(BeamDependencyIndexerTest, :"MixProject#{unique_id()}")
    dep_project_module = Module.concat(BeamDependencyIndexerTest, :"DepMixProject#{unique_id()}")
    dep_app = Keyword.get(opts, :dep_app, :beam_dep)
    dep_opts = Keyword.get(opts, :dep_opts, path: "deps/beam_dep")
    dep_tuple = {:beam_dep, dep_opts}

    File.mkdir_p!(app_root)

    File.write!(Path.join(app_root, "mix.exs"), """
    defmodule #{inspect(project_module)} do
      use Mix.Project

      def project do
        [app: :beam_dependency_indexer_test, version: "0.1.0", deps: deps()]
      end

      defp deps do
        #{inspect([dep_tuple])}
      end
    end
    """)

    Module.create(
      project_module,
      quote do
        def project do
          [app: :beam_dependency_indexer_test, version: "0.1.0", deps: deps()]
        end

        defp deps do
          unquote(Macro.escape([dep_tuple]))
        end
      end,
      Macro.Env.location(__ENV__)
    )

    project =
      app_root
      |> Forge.Document.Path.to_uri()
      |> Project.new()
      |> Project.set_project_module(project_module)

    {:ok, deps_root} = Engine.Mix.in_project(project, fn _ -> Mix.Project.deps_path() end)
    {:ok, build_path} = Engine.Mix.in_project(project, fn _ -> Mix.Project.build_path() end)
    dep_root = Path.join(deps_root, "beam_dep")
    dep_file = Forge.Path.native(Path.join([dep_root, "lib", "beam_dep_module.ex"]))
    ebin_path = Path.join([build_path, "lib", Atom.to_string(dep_app), "ebin"])

    File.mkdir_p!(Path.dirname(dep_file))
    File.mkdir_p!(ebin_path)

    File.write!(Path.join(dep_root, "mix.exs"), """
    defmodule #{inspect(dep_project_module)} do
      use Mix.Project

      def project do
        [app: #{inspect(dep_app)}, version: "0.1.0"]
      end
    end
    """)

    File.write!(dep_file, dep_source)

    compiler_options = Code.compiler_options()
    Code.compiler_options(debug_info: Keyword.get(opts, :debug_info?, true))
    on_exit(fn -> Code.compiler_options(compiler_options) end)

    compiled_modules = compile_to_path!([dep_file], ebin_path)
    expected_modules = Keyword.get(opts, :expected_modules, modules)

    assert Enum.sort(compiled_modules) == Enum.sort(expected_modules)

    if Keyword.get(opts, :rewrite_source?, true) do
      File.write!(dep_file, "defmodule")
      File.touch!(dep_file, {{2000, 1, 1}, {0, 0, 0}})
    end

    beam_paths = Enum.map(modules, &Path.join(ebin_path, Atom.to_string(&1) <> ".beam"))

    %{
      app_root: app_root,
      beam_path: hd(beam_paths),
      beam_paths: beam_paths,
      compiled_modules: compiled_modules,
      dep_file: dep_file,
      module: module,
      modules: modules,
      project: project
    }
  end

  defp with_transitive_beam_dependency(tmp_dir) do
    app_root = Path.join(tmp_dir, "beam_app")
    suffix = unique_id()
    direct_dep_app = :"beam_dep_#{suffix}"
    transitive_dep_app = :"transitive_dep_#{suffix}"
    direct_dep_path = "deps/#{direct_dep_app}"
    module = Module.concat(BeamDependencyIndexerTest, :"TransitiveDep#{suffix}")

    project_module = Module.concat(BeamDependencyIndexerTest, :"TransitiveMixProject#{suffix}")

    direct_dep_project_module =
      Module.concat(BeamDependencyIndexerTest, :"DirectDepMixProject#{suffix}")

    transitive_dep_project_module =
      Module.concat(BeamDependencyIndexerTest, :"TransitiveDepMixProject#{suffix}")

    File.mkdir_p!(app_root)

    File.write!(Path.join(app_root, "mix.exs"), """
    defmodule #{inspect(project_module)} do
      use Mix.Project

      def project do
        [app: :beam_dependency_indexer_test, version: "0.1.0", deps: deps()]
      end

      defp deps do
        [{#{inspect(direct_dep_app)}, path: #{inspect(direct_dep_path)}}]
      end
    end
    """)

    Module.create(
      project_module,
      quote do
        def project do
          [app: :beam_dependency_indexer_test, version: "0.1.0", deps: deps()]
        end

        defp deps do
          [{unquote(direct_dep_app), path: unquote(direct_dep_path)}]
        end
      end,
      Macro.Env.location(__ENV__)
    )

    project =
      app_root
      |> Forge.Document.Path.to_uri()
      |> Project.new()
      |> Project.set_project_module(project_module)

    {:ok, deps_root} = Engine.Mix.in_project(project, fn _ -> Mix.Project.deps_path() end)
    {:ok, build_path} = Engine.Mix.in_project(project, fn _ -> Mix.Project.build_path() end)

    direct_dep_root = Path.join(deps_root, Atom.to_string(direct_dep_app))
    transitive_dep_root = Path.join(deps_root, Atom.to_string(transitive_dep_app))
    transitive_file = Path.join([transitive_dep_root, "lib", "transitive_dep_module.ex"])
    ebin_path = Path.join([build_path, "lib", Atom.to_string(transitive_dep_app), "ebin"])

    File.mkdir_p!(direct_dep_root)
    File.mkdir_p!(Path.dirname(transitive_file))
    File.mkdir_p!(ebin_path)

    File.write!(Path.join(direct_dep_root, "mix.exs"), """
    defmodule #{inspect(direct_dep_project_module)} do
      use Mix.Project

      def project do
        [app: #{inspect(direct_dep_app)}, version: "0.1.0", deps: deps()]
      end

      defp deps do
        [{#{inspect(transitive_dep_app)}, path: "../#{transitive_dep_app}"}]
      end
    end
    """)

    File.write!(Path.join(transitive_dep_root, "mix.exs"), """
    defmodule #{inspect(transitive_dep_project_module)} do
      use Mix.Project

      def project do
        [app: #{inspect(transitive_dep_app)}, version: "0.1.0"]
      end
    end
    """)

    File.write!(transitive_file, """
    defmodule #{inspect(module)} do
      def public_fun, do: :ok
    end
    """)

    compiler_options = Code.compiler_options()
    Code.compiler_options(debug_info: true)
    on_exit(fn -> Code.compiler_options(compiler_options) end)

    assert [^module] = compile_to_path!([transitive_file], ebin_path)

    %{module: module, project: project}
  end

  defp compile_to_path!(files, ebin_path) do
    assert {:ok, compiled_modules, %{compile_warnings: [], runtime_warnings: []}} =
             Kernel.ParallelCompiler.compile_to_path(files, ebin_path, return_diagnostics: true)

    compiled_modules
  end

  defp expected_column(path, text) do
    line = line_containing(path, text)
    {byte_index, _length} = :binary.match(line, text)

    line
    |> binary_part(0, byte_index)
    |> String.length()
    |> Kernel.+(1)
  end

  defp line_containing(path, text) do
    path
    |> File.read!()
    |> String.split("\n")
    |> Enum.find(&String.contains?(&1, text))
  end

  defp unique_id do
    System.unique_integer([:positive])
  end

  defp native_join(path_segments) do
    path_segments
    |> Path.join()
    |> Forge.Path.native()
  end
end
